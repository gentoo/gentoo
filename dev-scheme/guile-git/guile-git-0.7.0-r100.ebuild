# Copyright 1999-2024 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

GUILE_COMPAT=( 2-2 3-0 )
inherit autotools guile

DESCRIPTION="Guile bindings of git"
HOMEPAGE="https://gitlab.com/guile-git/guile-git/"

if [[ "${PV}" == *9999* ]] ; then
	inherit git-r3

	EGIT_REPO_URI="https://gitlab.com/${PN}/${PN}.git"
else
	SRC_URI="https://gitlab.com/${PN}/${PN}/-/archive/v${PV}/${PN}-v${PV}.tar.bz2"
	S="${WORKDIR}/${PN}-v${PV}"

	KEYWORDS="~amd64 ~x86"
fi

LICENSE="LGPL-3+"
SLOT="0"

REQUIRED_USE="${GUILE_REQUIRED_USE}"

# <libgit2-1.8.0: https://gitlab.com/guile-git/guile-git/-/issues/30
RDEPEND="
	${GUILE_DEPS}
	<dev-libs/libgit2-1.8.0:=
	>=dev-libs/libgit2-1.3.0:=
	>=dev-scheme/bytestructures-2.0.2-r100[${GUILE_USEDEP}]
"
DEPEND="
	${RDEPEND}
"
BDEPEND="
	virtual/pkgconfig
"

src_prepare() {
	guile_src_prepare

	# network sandbox + ssh configuration
	sed -i -e '/tests\/clone.scm/d' Makefile.am || die

	eautoreconf
}

src_test() {
	guile_foreach_impl emake VERBOSE="1" check
}
