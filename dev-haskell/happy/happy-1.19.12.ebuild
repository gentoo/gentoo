# Copyright 1999-2022 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=7

# ebuild generated by hackport 0.6.9999
#hackport: flags: +small_base

CABAL_FEATURES="test-suite"
inherit haskell-cabal autotools

DESCRIPTION="Happy is a parser generator for Haskell"
HOMEPAGE="https://www.haskell.org/happy/"
SRC_URI="https://hackage.haskell.org/package/${P}/${P}.tar.gz"

LICENSE="BSD-2"
SLOT="0"
KEYWORDS="~amd64 ~arm64 ~ppc64 ~riscv ~x86"
IUSE="doc"

# Re: LLVM blocker - ghc-9.0.2 generates IR that crashes LLVM opt
# for two test cases.  Unsure yet if this is a bug in the GHC LLVM
# backend or in LLVM itself.  Tracked in
# https://github.com/llvm/llvm-project/issues/57393
#
# Must be in RDEPEND or pkgcheck complains about MisplaceWeakBlocker.
# Although in theory it's possible to enable USE=llvm on an arch with
# native codegen, there is no reason to do so, so in practice this should
# block USE=test exclusively on arches that support registerised GHC
# through the LLVM backend only.
RDEPEND=">=dev-haskell/mtl-2.2.1:=
	>=dev-lang/ghc-7.4.1:=
	test? ( !dev-lang/ghc[llvm(-)] )
"
DEPEND="${RDEPEND}
	>=dev-haskell/cabal-1.8
	doc? ( ~app-text/docbook-xml-dtd-4.2
			app-text/docbook-xsl-stylesheets )
"

src_prepare() {
	default

	if use doc; then
		cd doc || die
		eautoconf
	fi
}

src_configure() {
	# '--with-happy=false' allows detecting circular
	# depends even when 'happy' is installed in system
	haskell-cabal_src_configure \
		--with-happy=false\
		--flag=small_base

	if use doc; then
		# does not like out-of-source builds
		cd doc || die
		econf
	fi
}

src_compile() {
	haskell-cabal_src_compile

	if use doc; then
		emake -C doc -j1
	fi
}

src_install() {
	haskell-cabal_src_install

	if use doc; then
		docinto html
		dodoc -r doc/happy/.
		doman doc/happy.1
	fi
}
