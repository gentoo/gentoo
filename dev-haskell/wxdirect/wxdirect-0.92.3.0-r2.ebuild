# Copyright 1999-2021 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=7

# ebuild generated by hackport 0.5.3.9999

WX_GTK_VER="3.0-gtk3"

CABAL_FEATURES="lib profile haddock hoogle hscolour"
inherit haskell-cabal

DESCRIPTION="helper tool for building wxHaskell"
HOMEPAGE="https://wiki.haskell.org/WxHaskell"
SRC_URI="https://hackage.haskell.org/package/${P}/${P}.tar.gz"

LICENSE="BSD"
SLOT="${WX_GTK_VER}/${PV}"
KEYWORDS="~amd64 ~x86"
IUSE=""

RDEPEND=">=dev-haskell/parsec-2.1.0:=[profile?] <dev-haskell/parsec-4:=[profile?]
	dev-haskell/strict:=[profile?]
	>=dev-lang/ghc-7.4.1:=
	!!dev-haskell/wxdirect:3.0
"
DEPEND="${RDEPEND}
	>=dev-haskell/cabal-1.2
"

src_prepare() {
	default

	cabal_chdeps \
		'process    >= 1.1   && < 1.5' 'process    >= 1.1'\
		'base       >= 3     && < 4' 'base >=3'\
		'containers >= 0.1   && < 0.3' 'containers >=0.1'

	sed -e "s@executable wxdirect@executable wxdirect-${WX_GTK_VER}@" \
		-i "${S}/${PN}.cabal" \
		|| die "Could not change ${PN}.cabal for wxdirect slot ${WX_GTK_VER}"

	# embed ${CHOST}-cpp instead of "cpp" to support sys-devel/gcc-config[-native-symlinks]
	# Ideally it should also obey CPP variable
	sed -e 's/"cpp"/"'${CHOST}-cpp'"/g' \
		-i src/ParseC.hs || die
}
