# Copyright 1999-2018 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2

EAPI=6
inherit gnome2-utils qmake-utils

DESCRIPTION="A word unscrambling game"
HOMEPAGE="https://gottcode.org/connectagram/"
SRC_URI="https://gottcode.org/${PN}/${P}-src.tar.bz2"

LICENSE="GPL-3+"
SLOT="0"
KEYWORDS="~amd64 ~x86"
IUSE=""

RDEPEND="
	>=dev-qt/qtcore-5.2:5
	dev-qt/qtnetwork:5
	dev-qt/qtwidgets:5
	>=dev-qt/qtgui-5.2:5
"
DEPEND="${RDEPEND}"

src_prepare() {
	default

	eapply "${FILESDIR}"/${P}-gentoo.patch

	sed -i \
		-e "s#@GAMES_BINDIR@#/usr/bin#" \
		-e "s#@GAMES_DATADIR@#/usr/share#" \
		${PN}.pro src/{locale_dialog,new_game_dialog,wordlist}.cpp || die
}

src_configure() {
	eqmake5 connectagram.pro
}

src_install() {
	emake INSTALL_ROOT="${D}" install
	einstalldocs
}

pkg_preinst() {
	gnome2_icon_savelist
}

pkg_postinst() {
	gnome2_icon_cache_update
}

pkg_postrm() {
	gnome2_icon_cache_update
}
