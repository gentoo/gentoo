# Copyright 2021 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=7

inherit flag-o-matic multilib optfeature toolchain-funcs user-info

DESCRIPTION="Video Disk Recorder - turns a pc into a powerful set top box for DVB"
HOMEPAGE="http://www.tvdr.de/"
SRC_URI="ftp://ftp.tvdr.de/vdr/${P}.tar.bz2
	mainmenuhooks? ( http://vdr.websitec.de/download/${PN}/vdr-2.4.1/vdr-2.4.1_mainmenuhook-1.0.1.patch.bz2 )
	menuorg? ( https://projects.vdr-developer.org/projects/plg-menuorg/repository/revisions/master/raw/vdr-patch/vdr-menuorg-2.3.x.diff )
	naludump? ( http://www.udo-richter.de/vdr/files/vdr-2.1.5-naludump-0.1.diff )
	ttxtsubs? ( http://vdr.websitec.de/download/${PN}/vdr-2.4.4/vdr-2.4.4_ttxtsubs.patch.bz2 )"

LICENSE="GPL-2+"
SLOT="0"
KEYWORDS="~amd64 ~arm ~arm64 ~ppc ~x86"
IUSE="bidi debug demoplugins html keyboard mainmenuhooks menuorg naludump pinplugin permashift systemd ttxtsubs verbose"

COMMON_DEPEND="
	>=media-libs/fontconfig-2.4.2
	>=media-libs/freetype-2
	sys-libs/libcap
	virtual/jpeg:*
"
DEPEND="${COMMON_DEPEND}
	>=virtual/linuxtv-dvb-headers-5.3
"
RDEPEND="${COMMON_DEPEND}
	dev-lang/perl
	media-fonts/corefonts
	media-tv/gentoo-vdr-scripts
	bidi? ( dev-libs/fribidi )
	systemd? ( sys-apps/systemd )
"
BDEPEND="sys-devel/gettext"

REQUIRED_USE="pinplugin? ( !mainmenuhooks )
	permashift? ( !naludump !pinplugin )"

CONF_DIR="/etc/vdr"

pkg_setup() {
	use debug && append-flags -g

	PLUGIN_LIBDIR="/usr/$(get_libdir)/vdr/plugins"

	# read vdr user's $HOME from system - because old installations use /var/vdr/
	# while future installations with acct-user/vdr will use /var/lib/vdr/
	VIDEO_DIR="$(egethome vdr)/video"
	echo "VIDEO_DIR=$VIDEO_DIR"

	tc-export CC CXX AR
}

lang_po() {
	LING_PO=$( ls ${S}/po | sed -e "s:.po::g" | cut -d_ -f1 | tr \\\012 ' ' )
}

src_configure() {
	# support languages, written from right to left
	export "BIDI=$(usex bidi 1 0)"
	# systemd notification support
	export "SDNOTIFY=$(usex systemd 1 0)"
	# with/without keyboard
	export "USE_KBD=$(usex keyboard 1 0)"
	# detailed compile output for debug
	export "VERBOSE=$(usex verbose 1 0)"
}

src_prepare() {
	# apply maintenance-patches
	ebegin "Changing paths for gentoo"

	local DVBDIR=/usr/include
	local i
	for i in ${DVB_HEADER_PATH} /usr/include/v4l-dvb-hg /usr/include; do
		[[ -d ${i} ]] || continue
		if [[ -f ${i}/linux/dvb/dmx.h ]]; then
			einfo "Found DVB header files in ${i}"
			DVBDIR=${i}
			break
		fi
	done

	# checking for s2api headers
	local api_version
	api_version=$(awk -F' ' '/define DVB_API_VERSION / {print $3}' "${DVBDIR}"/linux/dvb/version.h)
	api_version=${api_version}*$(awk -F' ' '/define DVB_API_VERSION_MINOR / {print $3}' "${DVBDIR}"/linux/dvb/version.h)

	if [[ ${api_version:-0} -lt 5*3 ]]; then
		eerror "DVB header files do not contain s2api support or too old for ${P}"
		eerror "You cannot compile VDR against old dvb-header"
		die "DVB headers too old"
	fi

	cat > Make.config <<-EOT || die "cannot write to Make.config"
		#
		# Generated by ebuild ${PF}
		#
		PREFIX			= /usr
		DVBDIR			= ${DVBDIR}
		PLUGINLIBDIR	= ${PLUGIN_LIBDIR}
		CONFDIR			= ${CONF_DIR}
		ARGSDIR			= \$(CONFDIR)/conf.d
		VIDEODIR		= ${VIDEO_DIR}
		LOCDIR			= \$(PREFIX)/share/locale
		INCDIR			= \$(PREFIX)/include

		DEFINES			+= -DCONFDIR=\"\$(CONFDIR)\"
		INCLUDES		+= -I\$(DVBDIR)

		# >=vdr-1.7.36-r1; parameter only used for compiletime on vdr
		# PLUGINLIBDIR (plugin Makefile old) = LIBDIR (plugin Makefile new)
		LIBDIR			= ${PLUGIN_LIBDIR}
		PCDIR			= /usr/$(get_libdir)/pkgconfig

	EOT
	eend 0

	eapply "${FILESDIR}/vdr-2.4.1_gentoo.patch"
	use demoplugins || eapply "${FILESDIR}/vdr-2.4_remove_plugins.patch"
	eapply "${FILESDIR}/vdr-2.4.4_makefile-variables.patch"

	use naludump && eapply "${DISTDIR}/${PN}-2.1.5-naludump-0.1.diff"
	use permashift && eapply "${FILESDIR}/vdr-2.4.4-patch-for-permashift.diff"
	use pinplugin && eapply "${FILESDIR}/vdr-2.4.4_pinplugin.patch"
	use ttxtsubs && eapply "${WORKDIR}/vdr-2.4.4_ttxtsubs.patch"
	use menuorg && eapply "${DISTDIR}/vdr-menuorg-2.3.x.diff"
	use mainmenuhooks && eapply "${WORKDIR}/vdr-2.4.1_mainmenuhook-1.0.1.patch"

	# LINGUAS support
	einfo "\n \t VDR supports the LINGUAS values"

	lang_po

	einfo "\t Please set one of this values in your sytem make.conf"
	einfo "\t LINGUAS=\"${LING_PO}\"\n"

	if [[ -z ${LINGUAS} ]]; then
		einfo "\n \t No values in LINGUAS="
		einfo "\t You will get only english text on OSD \n"
	fi

	strip-linguas ${LING_PO} en

	default
}

src_install() {
	# trick the makefile to not create a VIDEODIR by supplying it with an
	# existing directory
	emake VIDEODIR="/" DESTDIR="${ED}" install

	keepdir "${PLUGIN_LIBDIR}"

	# backup for plugins they don't be able to create this dir
	keepdir "${CONF_DIR}/plugins"

	if use html; then
		local HTML_DOCS=( *.html )
	fi
	local DOCS=( MANUAL INSTALL README* HISTORY CONTRIBUTORS UPDATE-2* )
	einstalldocs

	insinto /usr/share/vdr
	doins "${FILESDIR}/capabilities.sh"

	fowners vdr:vdr "${CONF_DIR}" -R
}

pkg_postinst() {
	elog "Please read the /usr/share/doc/${PF}/UPDATE-2.4"
	elog "for major changes in this version\n"

	elog "It is a good idea to run vdrplugin-rebuild now.\n"

	elog "Additional recommendations to install:\n"
	optfeature "nice extra symbols in OSD (must select font VDRSymbolsSans in Setup)" media-fonts/vdrsymbols-ttf

	elog "\nTo get an idea how to proceed now, have a look at our vdr-guide:"
	elog "\thttps://wiki.gentoo.org/wiki/VDR"
}
