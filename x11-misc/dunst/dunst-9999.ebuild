# Copyright 1999-2020 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=7
inherit git-r3 toolchain-funcs

DESCRIPTION="Customizable and lightweight notification-daemon"
HOMEPAGE="https://dunst-project.org/ https://github.com/dunst-project/dunst"
EGIT_REPO_URI="https://github.com/${PN}-project/${PN}"

LICENSE="BSD"
SLOT="0"
KEYWORDS=""

DEPEND="
	dev-libs/glib:2
	sys-apps/dbus
	x11-libs/cairo[X,glib]
	x11-libs/gdk-pixbuf
	x11-libs/libX11
	x11-libs/libXScrnSaver
	x11-libs/libXinerama
	x11-libs/libXrandr
	x11-libs/libnotify
	x11-libs/pango[X]
"
BDEPEND="
	dev-lang/perl
	virtual/pkgconfig
"
RDEPEND="
	${DEPEND}
"

src_prepare() {
	sed -i -e "/^CFLAGS/ { s:-g::;s:-O.:: }" config.mk || die

	default
}

src_configure() {
	tc-export CC PKG_CONFIG
	default
}

src_install() {
	emake DESTDIR="${D}" PREFIX="/usr" install

	dodoc AUTHORS CHANGELOG.md README.md RELEASE_NOTES
}
