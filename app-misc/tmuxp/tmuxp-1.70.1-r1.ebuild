# Copyright 2019-2026 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

PYTHON_COMPAT=( python3_{11..15} )
DISTUTILS_USE_PEP517=hatchling
inherit distutils-r1

DESCRIPTION="tmux session manager. built on libtmux"
HOMEPAGE="https://tmuxp.git-pull.com"
SRC_URI="https://github.com/tmux-python/${PN}/archive/v${PV}.tar.gz -> ${P}.tar.gz"

LICENSE="MIT"
SLOT="0"
KEYWORDS="~amd64 ~arm ~arm64 ~ppc64 ~x86"

RDEPEND="
	>=app-misc/tmux-3.2
	=dev-python/libtmux-0.58.1*[${PYTHON_USEDEP}]
	>=dev-python/pyyaml-6.0[${PYTHON_USEDEP}]
"
BDEPEND="
	test? (
		>=dev-python/pytest-6.2.5[${PYTHON_USEDEP}]
		>=dev-python/pytest-mock-3.14.0[${PYTHON_USEDEP}]
		>=dev-python/pytest-rerunfailures-4.2[${PYTHON_USEDEP}]
	)
"

distutils_enable_tests pytest

PATCHES=(
	"${FILESDIR}/tmuxp-1.74.0_theme-name.patch"
)

python_prepare_all() {
	distutils-r1_python_prepare_all
}

python_test() {
	local -a EPYTEST_DESELECT=(
		# test doesn't get along with sandbox
		"tests/cli/test_load.py::test_load_zsh_autotitle_warning"
	)

	local -a EPYTEST_IGNORE=(
		# not actually tests, but throws off test collection
		"tests/fixtures/"
	)

	local -a EPYTEST_PLUGINS=(
		pytest-mock
		pytest-rerunfailures
	)

	# don't run doc tests as they need a bunch of extra sphinx stuff
	rm -rf tests/docs || die

	SHELL="/bin/bash" epytest tests
}
